apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
    compileSdkVersion compileSdk

    signingConfigs {
        release {
            file("../signing.properties").with { propFile ->
                if (propFile.canRead()) {
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))

                    storeFile file(properties['keystorePath'])
                    storePassword properties['keystorePassword']
                    keyAlias properties['keyAlias']
                    keyPassword properties['keyPassword']
                } else {
                    println 'Unable to read signing.properties'
                }
            }
        }
    }

    defaultConfig {
        applicationId "com.jonasgerdes.stoppelmap"
        minSdkVersion minSdk
        targetSdkVersion targetSdk
        versionCode 35
        versionName "v2019.2.1-${gitSha}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }

}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':core')
    implementation project(':widgets')
    implementation project(':data')
    implementation project(':home')
    implementation project(':map')
    implementation project(':about')
    implementation project(':news')
    implementation project(':androidutil')

    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines
    implementation deps.kotlin.coroutines_android

    implementation deps.firebase.core
    implementation deps.firebase.messaging

    implementation deps.androidx.compat
    implementation deps.androidx.material
    implementation deps.androidx.ktx
    implementation deps.androidx.constraint_layout

    implementation deps.koin.core
    implementation deps.koin.android
    implementation deps.koin.viewmodel

    implementation deps.threeten_android

    implementation deps.lottie
}

apply plugin: 'com.google.gms.google-services'
