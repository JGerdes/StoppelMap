apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

android {
    compileSdkVersion rootProject.ext.compileSdk

    signingConfigs {
        release {
            file("../signing.properties").with { propFile ->
                if (propFile.canRead()) {
                    def properties = new Properties()
                    properties.load(new FileInputStream(propFile))

                    storeFile file(properties['keystorePath'])
                    storePassword properties['keystorePassword']
                    keyAlias properties['keyAlias']
                    keyPassword properties['keyPassword']
                } else {
                    println 'Unable to read signing.properties'
                }
            }
        }
    }

    defaultConfig {
        applicationId "com.jonasgerdes.stoppelmap"
        minSdkVersion rootProject.ext.minSdk
        targetSdkVersion rootProject.ext.targetSdk
        versionCode 40
        versionName "v2019.4.2-${gitSha}"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility 1.8
        targetCompatibility 1.8
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation project(':core')
    implementation project(':widgets')
    implementation project(':data')
    implementation project(':home')
    implementation project(':map')
    implementation project(':events')
    implementation project(':transport')
    implementation project(':news')
    implementation project(':about')
    implementation project(':androidutil')

    implementation deps.kotlin.stdlib
    implementation deps.kotlin.coroutines
    implementation deps.kotlin.coroutines_android

    implementation deps.playServices.location

    implementation deps.firebase.core
    implementation deps.firebase.messaging

    implementation deps.androidx.compat
    implementation deps.androidx.material
    implementation deps.androidx.ktx
    implementation deps.androidx.constraint_layout

    implementation deps.dagger
    kapt deps.dagger_compiler

    implementation deps.threeten_android

    implementation deps.lottie

    // needed by dagger (https://github.com/google/dagger/issues/970)
    compileOnly deps.moshi
    compileOnly deps.androidx.arch.room.runtime
    compileOnly deps.retrofit.core

    testImplementation deps.test.junit
    testImplementation deps.threeten
}

apply plugin: 'com.google.gms.google-services'
